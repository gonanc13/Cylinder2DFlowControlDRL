// Define parameters. All Non-Dimensional (ND). These can then be quickly modified within the code.
DefineConstant[
jets_toggle = {1, Name "Toggle Jets --> 0 : No jets, 1: Yes jets"}
height_cylinder = {1, Name "Cylinder Height (ND)"}
ar = {1, Name "Cylinder Aspect Ratio"}
cylinder_y_shift = {0, Name "Cylinder Center Shift from Centerline, Positive UP (ND)"}
x_upstream = {10, Name "Domain Upstream Length (from left-most rect point) (ND)"}
x_downstream = {30, Name "Domain Downstream Length (from right-most rect point) (ND)"}
height_domain = {20, Name "Domain Height (ND)"}
mesh_size_cylinder = {0.01, Name "Mesh Size on Cylinder Walls"}
mesh_size_wall = {0.05, Name "Mesh Size on Channel Walls"}
mesh_size_coarse = {0.1, Name "Mesh Size Close to Outflow"}
coarse_distance = {5, Name "Distance From Cylinder's Right-Most Point Where Coarsening Starts (ND)"}
jet_width = {0.1, Name "Jet Width (ND)"}
];

// Seed the cylinder's center's identifier and create the center point
center = newp;
Point(center) = {0, 0, 0, mesh_size_cylinder};

// Cylinder dimensions
r_height = height_cylinder; // Cylinder height
r_length = ar*height_cylinder; // Cylinder length


// Start definition of cylinder surfaces (curves). Note: it is defined in CCW sense
// Each jet surface is a physical line, and the remaining of the rectangle is another

// Define x,y coors of the rectangle sides
y_top = r_height/2;
y_bot = -r_height/2;
x_left = -r_length/2;
x_right = r_length/2;

// Define x coors of jets centres and upstream bound
x_jet_centre = x_right-jet_width/2;
x_jet_leftbound = x_right-jet_width;

// Define all points of rectangle
p = newp;
Point(p) = {x_right, y_top, 0, mesh_size_cylinder};  // Top right corner (p)
Point(p+1) = {x_jet_centre, y_top, 0, mesh_size_cylinder};  // Top jet centre (p+1)
Point(p+2) = {x_jet_leftbound, y_top, 0, mesh_size_cylinder};  // Top jet upstream bound (p+2)
Point(p+3) = {x_left, y_top, 0, mesh_size_cylinder};  // Top left corner (p+3)
Point(p+4) = {x_left, y_bot, 0, mesh_size_cylinder};  // Bottom left corner (p+4)
Point(p+5) = {x_jet_leftbound, y_bot, 0, mesh_size_cylinder};  // Bottom jet upstream bound (p+5)
Point(p+6) = {x_jet_centre, y_bot, 0, mesh_size_cylinder};  // Bottom jet centre (p+6)
Point(p+7) = {x_right, y_bot, 0, mesh_size_cylinder};  // Bottom right corner (p+7)

If(jets_toggle)

  cylinder[] = {}; // Create empty list of curves (surfaces) of the cylinder. Defined CCW
  no_slip_cyl[] = {};  // No-slip cylinder physical surfaces list

  // Define top jet surface:
  l = newl;
  Line(l) = {p, p+1};
  Line(l+1) = {p+1, p+2};
  Physical Line(5) = {l, l+1};  // Top jet physical surface
  cylinder[] += {l, l+1}; // Add to cylinder list

  // Define left no-slip surface of cylinder
  l = newl;
  Line(l) = {p+2, p+3};
  Line(l+1) = {p+3, p+4};
  Line(l+2) = {p+4, p+5};
  no_slip_cyl[] += {l, l+1, l+2};
  cylinder[] += {l, l+1, l+2};

  // Define bottom jet surface:
  l = newl;
  Line(l) = {p+5, p+6};
  Line(l+1) = {p+6, p+7};
  Physical Line(6) = {l, l+1};  // Bottom jet physical surface
  cylinder[] += {l, l+1}; // Add to cylinder list

  // Define right no-slip surface
  l = newl;
  Line(l) = {p+7, p};
  no_slip_cyl[] += {l};
  cylinder[] += {l}; // Add to cylinder list

  Physical Line(4) = {no_slip_cyl[]};  // Define no-slip cylinder physical surfaces

// Just the rectangle (if number no jets)
Else

   l = newl;
   Line(l) = {p, p+3};
   Line(l+1) = {p+3, p+4};
   Line(l+2) = {p+4, p+7};
   Line(l+3) = {p+7, p};

   cylinder[] = {l, l+1, l+2, l+3};	// List of curves (surfaces) of the cylinder. Defined CCW
   Physical Line(4) = {cylinder[]}; // Define no-slip cylinder physical surfaces (in this case all cyl)
EndIf

// Create the channel (Domain exterior boundary)
// Define useful quantities
y_top = height_domain/2-cylinder_y_shift;  // Smaller than half the height if positive shift
y_bot = -height_domain/2-cylinder_y_shift; // Larger in mag than half the height if positive shift
x_left = -r_length/2-x_upstream;
x_right = r_length/2+x_downstream;
x_coarse = r_length/2+coarse_distance;

// Define points
p = newp;
Point(p) = {x_left, y_bot, 0, mesh_size_wall}; // bottom-left corner
Point(p+1) = {x_coarse, y_bot, 0, mesh_size_coarse}; // bottom start of coarse zone
Point(p+2) = {x_right, y_bot, 0, mesh_size_coarse}; // bottom-right corner
Point(p+5) = {x_left, y_top, 0, mesh_size_wall}; // top-left corner
Point(p+4) = {x_coarse, y_top, 0, mesh_size_coarse}; // top start of coarse zone
Point(p+3) = {x_right, y_top, 0, mesh_size_coarse}; // top-right corner

l = newl;
// Bottom wall (no slip)
Line(l) = {p, p+1};
Line(l+1) = {p+1, p+2};
Physical Line(1) = {l, l+1};

// Right wall (outflow)
Line(l+2) = {p+2, p+3};
Physical Line(2) = {l+2};

// Top wall (no slip)
Line(l+3) = {p+3, p+4};
Line(l+4) = {p+4, p+5};
Physical Line(1) += {l+3, l+4};

// Inlet
Line(l+5) = {p+5, p};
Physical Line(3) = {l+5};

// Coarse line
Line(l+6) = {p+1, p+4};

// Define coarse mesh portion of domain
// Create line loop for coarse area
coarse = newll;
Line Loop(coarse) = {(l+1), (l+2), (l+3), -(l+6)};
// Create surface and physical surface for coarse area
s = news;
Plane Surface(s) = {coarse};
Physical Surface(1) = {s};  // Physical surface to be mesh (then we'll add fine portion)

// Define fine mesh portion of domain
// Create list of lines that contain the cylinder (== fine mesh zone)
cframe[] = {l, (l+6), l+4, l+5};
outer = newll;
Line Loop(outer) = {cframe[]}; // Outer line loop
inner = newll;
Line Loop(inner) = {cylinder[]}; // Inner line loop (cylinder)

// Define final physical surface
s = news;
Plane Surface(s) = {outer, inner}; // Should be outer, inner, no??
Physical Surface(1) += {s}; // // Add to surface to be mesh

// First the jet and no slip surfaces of the cylinder are defined. Each jet surface is a physical line and all the no slip
// cylinder surfaces are another. Then the domain is created.